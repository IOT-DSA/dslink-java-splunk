apply plugin: 'application'
apply plugin: 'findbugs'
apply plugin: 'java'
apply plugin: 'idea'

mainClassName = 'org.dsa.iot.splunk.Main'
sourceCompatibility = 1.7
targetCompatibility = 1.7
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        url 'http://repo.spring.io/libs-release-remote'
    }
}

configurations {
    provided
}

idea {
    module {
        scopes.PROVIDED.plus += [ configurations.provided ]
    }
}

dependencies {
    provided 'com.google.code.findbugs:annotations:3.0.0'
    compile 'org.iot-dsa:dslink:0.8.0'

    // Splunk dependencies
    compile 'com.splunk:splunk:1.4.0.0'
}

compileJava {
    sourceSets.main.compileClasspath += configurations.provided
}

run {
    args System.getProperty("exec.args", "").split()
    workingDir project.buildDir
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

applicationDistribution.from(new File(project.projectDir, "/dslink.json"))
